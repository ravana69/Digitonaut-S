<!DOCTYPE html>
<html lang="en">

<!-- Mirrored from digitonaut.com/art/signed_distance_fields/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:33:32 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Signed Distance Fields</title>

<script src="shader-web-background.min.js"></script>

<script type="x-shader/x-fragment" id="Image">

// Signed Distance Fields

precision highp float;
uniform vec2  iResolution;
uniform float iTime;

// -------------------------------------------------------------------------------------------------- Paste your Shadertoy code here:
// Uncomment for debug rendering
//#define COMPLEXITY
//#define ISOLINES
//#define CURVATURE

float delta = 0.0001;
float delta2 = 0.1;
float maxT = 20.0;
//vec4 bgColor = vec4(0.25,0.5,0.8,1.0);
vec4 bgColor = vec4(1.0,1.0,1.0,1.0);

// SDF primitives
// giving primitives a position makes them easy to use
// but more complex in calculation and difficult to use with manipulations like extrude
float sdSphere(vec3 pos, float r, in vec3 p) {
    return distance(p, pos) - r;
}
float sdBox(vec3 pos, vec3 ext, in vec3 p) {
// ok for inner part, but sharp outside and too long distance at corners
// works, but incompatible with rounding
/*    return max(max(abs(p.x - pos.x) - ext.x/2.0, 
                   abs(p.y - pos.y) - ext.y/2.0),
                   abs(p.z - pos.z) - ext.z/2.0);*/
    vec3 q = abs(p-pos) - ext/2.0;
    return length(max(q,0.0)) + min(max(max(q.x, q.y),q.z),0.0);
}
float sdTorus(in vec3 p, float r1, float r2) {
    vec3 c = vec3(normalize(p.xy)*r1, 0.0);
    return distance(p, c) - r2;
}

// SDF operations
float sdUnion(float a, float b) {return min(a,b);}
float sdIntersect( float a, float b) {return max(a,b);}
float sdSubstract(float a, float b) {return max(a, -b);}

float sdShell(float d, float t) {return abs(d)-t;}

vec3 sdExtrude(vec3 pos, vec3 dir) {
    return pos - clamp(pos, -0.5*dir, 0.5*dir);
}
vec3 sdArray(vec3 pos, vec3 step) {
    // we dont want the break in the center
    // the repeaded block should be centered at origin
    return mod(pos +0.5*step, step) - 0.5*step;
}
vec3 sdMirror(vec3 pos) {
    return abs(pos);
}

float isoLines(float d, float seg) {
    return mod(d, seg) > seg*0.95 ^^ d < 0.0?1.0:0.0;
}

float map(in vec3 p) {
    float d1 = sdShell(sdSphere(vec3(0,0,0), 0.5, sdExtrude(sdArray(p-vec3(sin(iTime), cos(iTime), 0.0), vec3(2,2,2)), vec3(0,1,0))), 0.1);
    float d2 = sdBox(vec3(cos(iTime), sin(iTime), sin(iTime)), vec3(8.0,2.0,12), p) - 1.0;
    float d = sdSubstract(d1,d2);
    //d = sdUnion(d, sdTorus(sdMirror(p - vec3(cos(iTime), sin(iTime), sin(iTime)-5.0)) - vec3(3,0,1), 2.0, 1.0));
    
    return d;
}

// calculate normal based on simplified(3 vs. 6 samples) gradient around pos
// Note: when we are marching solids, d is 0 on surface
vec3 calcNormal(in vec3 p, in float d) {
    float dx = map(p + vec3(delta,0,0));
    float dy = map(p + vec3(0,delta,0));
    float dz = map(p + vec3(0,0,delta));
    
    return normalize(vec3(dx-d, dy-d, dz-d));
}

float calcCurvature(in vec3 p, in float d) {
    float dx = map(p + vec3(delta2,0,0));
    float dy = map(p + vec3(0,delta2,0));
    float dz = map(p + vec3(0,0,delta2));
    
    float ndx = map(p + vec3(-delta2,0,0));
    float ndy = map(p + vec3(0,-delta2,0));
    float ndz = map(p + vec3(0,0,-delta2));
    
    
    return clamp((dx+dy+dz+ndx+ndy+ndz-6.0*d)/delta2/4.0,-1.0,1.0);
}

void render(out vec4 fragColor, in vec3 ro, in vec3 rd) {
    float t = 0.0;
    
    int j = 0;
    for(int i = 0; i < 100; i++) {
        if (t > maxT) {
            fragColor = bgColor;
            break;
        }
        
        float d = map(ro+t*rd);
        if(d < delta || i == 99) {
            vec3 n = calcNormal(ro+t*rd, d);
            fragColor = vec4((n*0.5+0.5)*min(calcCurvature(ro+t*rd, d)+1.0, 1.0),1.0);
#ifdef CURVATURE
    fragColor = mix(vec4(0,0,1,1), vec4(1,0,0,1), calcCurvature(ro+t*rd, d) *0.5 + 0.5);
#endif
            break;
        } else {
            t += d;
        }
        j++;
    }
    // fade to bg (fog)
    fragColor = mix(fragColor, bgColor, t/maxT);
    
#ifdef COMPLEXITY //marching complexity
    fragColor = mix(vec4(0,0,1,1), vec4(1,0,0,1), float(j)/100.0);
#endif
}

void debugRender(out vec4 fragColor, in vec2 uv) {
    float d = map(vec3(uv,0));
    vec3  n = calcNormal(vec3(uv, 0), d);
    float l = isoLines(d, 0.1);

    // colored
    //fragColor = vec4((n*0.5+0.5)*l,1.0);
    
    // monochrome
    fragColor = vec4(l,l,l,1.0);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    // Normalized pixel coordinates (from 0 to 1)
    vec2 uv = (fragCoord-iResolution.xy/2.0)/iResolution.y*2.0;
    
#ifdef ISOLINES
    debugRender(fragColor, uv);
    //fragColor = vec4(mod(uv, 1.0), 0,1);
#else
    vec3 ro = vec3(0,0,1);
    vec3 rd = normalize(vec3(uv.xy, 0) - ro);
    render(fragColor, ro , rd);
#endif


}

void mainVR( out vec4 fragColor, in vec2 fragCoord, in vec3 fragRayOri, in vec3 fragRayDir ) {

    render(fragColor, fragRayOri, fragRayDir);
}

// -------------------------------------------------------------------------------------------------- End of Shadertoy code


void main() {
mainImage(gl_FragColor, gl_FragCoord.xy);
}
</script>
<script>
shaderWebBackground.shade({
	shaders: {
		Image: {
			uniforms: {
				iResolution: (gl, loc, ctx) => gl.uniform2f(loc, ctx.width, ctx.height),
				iTime: (gl, loc) => gl.uniform1f(loc, performance.now() / 1000)
			}
		}
	}
});
</script>
</head>
<body>
</body>

<!-- Mirrored from digitonaut.com/art/signed_distance_fields/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:33:32 GMT -->
</html>
