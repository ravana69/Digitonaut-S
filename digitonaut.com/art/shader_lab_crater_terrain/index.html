
<!DOCTYPE html>
<html>
   
<!-- Mirrored from digitonaut.com/art/shader_lab_crater_terrain/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:32:59 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Shader Lab Crater Terrain</title>

<!--
   Ray Marching demo by Kevin Roast
   
   - Lots of code/ideas for this demo come from reading:
     http://www.iquilezles.org/www/material/nvscene2008/rwwtt.pdf
     http://www.pouet.net/topic.php?which=7931&page=1
     http://www.pouet.net/topic.php?which=7920&page=1
     http://iquilezles.org/www/articles/distfunctions/distfunctions.htm
     https://www.shadertoy.com/
   
   If you want to understand this stuff read through the PDF and pouet.net links above!
-->
<script src="utils.js"></script>
<script id="vertex" type="x-shader">
attribute vec2 aVertexPosition;

void main()
{
   gl_Position = vec4(aVertexPosition, 0.0, 1.0);
}
      </script>
      <script id="fragment" type="x-shader">
#ifdef GL_ES
precision highp float;
#endif

uniform float time;
uniform vec2 resolution;
uniform vec3 cameraPos;
uniform vec3 cameraLookat;
uniform vec3 sunDir;
uniform vec3 sunColour;
uniform float ambient;
uniform bool ao;
uniform bool shadows;
uniform bool postEffects;
uniform bool moveCamera;
uniform bool fog;
uniform float fogC;
uniform float fogB;
uniform float snowLevel;
uniform float grassLevel;
uniform float lacunarity;
uniform float amplitude;
uniform float offset;
uniform float squeeze;
uniform float param;
uniform sampler2D texture0;

//#define AA

#define GAMMA 0.8
#define CONTRAST 1.2
#define SATURATION 1.4
#define BRIGHTNESS 1.4
#define SHADOW_RAY_DEPTH 64
#define AO_SAMPLES 4
#ifndef AA
#define RAY_DEPTH 200
#define MAX_DEPTH 50.0
#define FRACT_NOISE_DEPTH 6
#define DISTANCE_MIN 0.003
#else
#define RAY_DEPTH 275
#define MAX_DEPTH 60.0
#define FRACT_NOISE_DEPTH 8
#define DISTANCE_MIN 0.002
#endif
#define PI 3.14159265

const vec2 delta = vec2(DISTANCE_MIN, 0.);

const vec3 skyColour = vec3(.1, .2, .3);
const vec3 horizColour = vec3(.38, .36, .32);

#define TEXTURESIZE 512.0

float NoiseL(in vec2 x)
{
   return texture2D(texture0,(x+vec2(0.5,0.5))/512.0,-100.0).x;
}

//	FAST32_hash
//	A very fast hashing function.  Requires 32bit support.
//	http://briansharpe.wordpress.com/2011/11/15/a-fast-and-simple-32bit-floating-point-hash-function/
void FAST32_hash_2D( vec2 gridcell, out vec4 hash_0, out vec4 hash_1 )
{
   // gridcell is assumed to be an integer coordinate
   const vec2 OFFSET = vec2( 26.0, 161.0 );
   const float DOMAIN = 71.0;
   const vec2 SOMELARGEFLOATS = vec2( 951.135664, 642.949883 );
   vec4 P = vec4( gridcell.xy, gridcell.xy + 1.0 );
   P = P - floor(P * ( 1.0 / DOMAIN )) * DOMAIN;
   P += OFFSET.xyxy;
   P *= P;
   P = P.xzxz * P.yyww;
   hash_0 = fract( P * ( 1.0 / SOMELARGEFLOATS.x ) );
   hash_1 = fract( P * ( 1.0 / SOMELARGEFLOATS.y ) );
}

vec4 Interpolation_C2_InterpAndDeriv( vec2 x )
{
   return x.xyxy * x.xyxy * ( x.xyxy * ( x.xyxy * ( x.xyxy * vec2( 6.0, 0.0 ).xxyy + vec2( -15.0, 30.0 ).xxyy ) + vec2( 10.0, -60.0 ).xxyy ) + vec2( 0.0, 30.0 ).xxyy );
}


//	Perlin2D_Deriv
//	Classic Perlin 2D noise with derivatives
//	returns vec3( value, xderiv, yderiv )
vec3 NoiseD( vec2 P )
{
    //  https://github.com/BrianSharpe/Wombat/blob/master/Perlin2D_Deriv.glsl

    // establish our grid cell and unit position
    vec2 Pi = floor(P);
    vec4 Pf_Pfmin1 = P.xyxy - vec4( Pi, Pi + 1.0 );

    //	calculate the hash.
    //	( various hashing methods listed in order of speed )
    vec4 hash_x, hash_y;
    FAST32_hash_2D( Pi, hash_x, hash_y );

    //	calculate the gradient results
    vec4 grad_x = hash_x - 0.49999;
    vec4 grad_y = hash_y - 0.49999;
    vec4 norm = inversesqrt( grad_x * grad_x + grad_y * grad_y );
    grad_x *= norm;
    grad_y *= norm;
    vec4 dotval = ( grad_x * Pf_Pfmin1.xzxz + grad_y * Pf_Pfmin1.yyww );

    //	Convert our data to a more parallel format
    vec3 dotval0_grad0 = vec3( dotval.x, grad_x.x, grad_y.x );
    vec3 dotval1_grad1 = vec3( dotval.y, grad_x.y, grad_y.y );
    vec3 dotval2_grad2 = vec3( dotval.z, grad_x.z, grad_y.z );
    vec3 dotval3_grad3 = vec3( dotval.w, grad_x.w, grad_y.w );

    //	evaluate common constants
    vec3 k0_gk0 = dotval1_grad1 - dotval0_grad0;
    vec3 k1_gk1 = dotval2_grad2 - dotval0_grad0;
    vec3 k2_gk2 = dotval3_grad3 - dotval2_grad2 - k0_gk0;

    //	C2 Interpolation
    vec4 blend = Interpolation_C2_InterpAndDeriv( Pf_Pfmin1.xy );

    //	calculate final noise + deriv
    vec3 results = dotval0_grad0
                    + blend.x * k0_gk0
                    + blend.y * ( k1_gk1 + blend.x * k2_gk2 );

    results.yz += blend.zw * ( vec2( k0_gk0.x, k1_gk1.x ) + blend.yx * k2_gk2.xx );

    return results * 1.4142135623730950488016887242097;  // scale things to a strict -1.0->1.0 range  *= 1.0/sqrt(0.5)
}

const mat2 octave = mat2(2.0,0.8,-0.8,2.0);
#define AMPLITUDE 0.5
#define LACUNARITY 2.333
// ridge offset - see http://thebookofshaders.com/13/
#define OFFSET 1.55
float FractalNoiseM(in vec2 xy)
{
   float freq = 1.0;
   float a = amplitude;
   float f = 0.0;
   vec3 n = vec3(0.0);
   vec2 dsum = vec2(0,0);
   for (int i = 0; i < FRACT_NOISE_DEPTH-1; i++)
   {
      n = NoiseD((xy + squeeze * dsum) * freq);
      f += a * (offset - abs(n.x));
      dsum += a * n.yz * -n.x;
      freq *= lacunarity;
      a *= 0.5 * clamp(f,0.0,1.0);
      //xy *= octave; // NOTE: if doing this, lanc=1.2, amp=0.65, offset=1.0
   }
   return f;
}

float FractalNoiseH(in vec2 xy)
{
   float freq = 1.0;
   float a = amplitude;
   float f = 0.0;
   vec3 n = vec3(0.0);
   vec2 dsum = vec2(0,0);
   for (int i = 0; i < FRACT_NOISE_DEPTH; i++)
   {
      n = NoiseD((xy + squeeze * dsum) * freq);
      f += a * (offset - abs(n.x));
      dsum += a * n.yz * -n.x;
      freq *= lacunarity;
      a *= 0.5 * clamp(f,0.0,1.0);
      //xy *= octave;
   }
   return f;
}

float Terrain(vec2 pos)
{
   return FractalNoiseH(pos.yx*0.171)*1.693;// + NoiseL(cos(pos.yx*param)+sin(pos.xy*param));
}

float TerrainM(vec2 pos)
{
   return FractalNoiseM(pos.yx*0.171)*1.693;// + NoiseL(cos(pos.yx*param)+sin(pos.xy*param));
}

// This should return continuous positive values when outside and negative values inside,
// which roughly indicate the distance of the nearest surface.
// NOTE! for increased performance, but lower Shadow/AO/intersect accuracy, change Terrain() to TerrainM() here
float Dist(vec3 pos)
{
   return pos.y - TerrainM(pos.xz);
}

vec3 CalcNormal(in vec3 pos, float t)
{
   // NOTE: subtle changes to this value affect the sharpness of the normal - 0.001 is about same as GetNormal() usually
   vec2 eps = vec2(0.001*t, 0.0);
   return normalize( vec3( Terrain(pos.xz-eps.xy) - Terrain(pos.xz+eps.xy),
                           2.0*eps.x,
                           Terrain(pos.xz-eps.yx) - Terrain(pos.xz+eps.yx) ) );
}

vec3 Sky(in vec3 ro, in vec3 rd)
{
   // background sky     
   vec3 col = 0.9*vec3(0.4,0.5,0.70) - rd.y*vec3(0.4,0.36,0.45);
   col = mix(col, vec3(1.0, 0.9, 0.9), 0.4);

   // sun glare    
   float sun = clamp( dot(sunDir,rd), 0.0, 1.0 );
   col += 0.6*vec3(1.0,0.6,0.3)*pow( sun, 64.0 );

   return col;
}

vec3 DepthFog(in vec3  rgb,in float distance, in vec3 rd, in vec3 ro)
{
   float fogAmount = 0.0;
   if (fog) fogAmount = fogC * exp(-ro.y*fogB) * (1.0-exp(-distance*rd.y*fogB))/rd.y;
   return mix(rgb, Sky(ro, rd), clamp(fogAmount, 0.0, 1.0));
}

// Based on original code by IQ
const float spread = 24.0;
float SoftShadow(vec3 ro, vec3 rd)
{
   float res = 1.0;
   float t = 0.01;    // min-t see http://www.iquilezles.org/www/articles/rmshadows/rmshadows.htm
   for (int i=0; i<SHADOW_RAY_DEPTH; i++)
   {
      if (t < 16.0)  // max-t
      {
         float h = Dist(ro + rd * t);
         res = min(res, spread*h/t);
         t += h;
      }
   }
   return clamp(res, 0.0, 1.0);
}

// Based on original code by IQ
float CalcAO(vec3 p, vec3 n)
{
   float r = 0.0;
   float w = 1.0;
   for (int i=1; i<=AO_SAMPLES; i++)
   {
      float d0 = float(i) * 0.2;
      r += w * (d0 - Dist(p + n * d0));
      w *= 0.5;
   }
   return 1.0 - clamp(r,0.0,1.0);
}

// "Natural light" shading - main sun light with soft shadows, sky light with AO and indirect sun bounce light with AO
// see http://iquilezles.org/www/articles/outdoorslighting/outdoorslighting.htm
const vec3 rockColour  = vec3(0.8, 0.82, 0.82);
const vec3 peaksColour = vec3(1.0, 0.95, 0.95);
const vec3 grassColour = vec3(0.5,0.4,0.3);
const float specularHardness = 128.0;
const float specular = 1.00;
vec3 Shading(vec3 pos, vec3 rd, vec3 norm, vec3 ro)
{
   float sun = clamp(dot(norm, sunDir), 0.0, 1.0);
   vec3 heading = normalize(-rd + sunDir);
   
   // start with rock colour, add in snow on peaks and grass in valleys
   float dif = 0.5;
   float snow = NoiseL(norm.yz*1.111) + snowLevel;
   float grass = NoiseL(norm.xy*0.973) - grassLevel;
   
   vec3 colour = rockColour;
   float spec = 0.2;
   float specBy = pow(max(0.0, dot(heading, norm)), specularHardness);
   
   // give a more abrubt cut-off where snow is mixed into rock colour
   // snow also has a specular reflective component
   float mixSnow = smoothstep(0.05, 0.5, pos.y - snow);
   colour = mix(colour, peaksColour, mixSnow);
   spec = mix(0.0, specBy, mix(spec, 0.0, mixSnow));
   dif = mix(dif, 0.9, mixSnow);

   // mix grass colour
   float mixGrass = 1.0 - clamp(pos.y + grass - norm.y, 0.0, 1.0);
   colour = mix(colour, grassColour, mixGrass);
   spec = mix(0.0, specBy, mix(spec, 0.0, mixGrass));
   dif = mix(dif, 0.8, mixGrass);
   
   vec3 light = sun * sunColour * dif + (spec * specular);

   if (shadows) {
      // colorize penumbras
      light *= pow(vec3(SoftShadow(pos, sunDir)), vec3(1.0, 1.2, 1.5));
   }
   
   // sky light with AO
   float occ = ambient;
   if (ao) occ = CalcAO(pos, norm) * ambient;
   float sky = clamp(0.5 + 0.5 * norm.y, 0.0, 1.0);
   light += sky * vec3(0.24, 0.20, 0.24) * occ;
   
   // indirect light with AO
   float ind = clamp(dot(norm, normalize(sunDir * vec3(-1.0,0.0,-1.0))), 0.0, 1.0);
   light += ind * colour * 0.3 * occ;
   
   colour *= light;
   
   // fogging
   colour = DepthFog(colour, length(ro-pos), rd, ro);
   
   return colour;
}

// Original method by David Hoskins
vec3 PostEffects(vec3 rgb, vec2 xy)
{
   rgb = pow(rgb, vec3(GAMMA));
   rgb = mix(vec3(.5), mix(vec3(dot(vec3(.2125, .7154, .0721), rgb*BRIGHTNESS)), rgb*BRIGHTNESS, SATURATION), CONTRAST);
   rgb *= .4+0.5*pow(40.0*xy.x*xy.y*(1.0-xy.x)*(1.0-xy.y), 0.2 ); 
   return rgb;
}

// Camera function by TekF
// Compute ray from camera parameters
vec3 GetRay(vec3 dir, vec2 pos)
{
   pos = pos - 0.5;
   pos.x *= resolution.x/resolution.y;
   
   dir = normalize(dir);
   vec3 right = normalize(cross(vec3(0.,1.,0.),dir));
   vec3 up = normalize(cross(dir,right));
   
   return dir + right*pos.x + up*pos.y;
}

#define ANTIALIAS_SAMPLES 8
void main()
{
   vec3 off = vec3(0.0);
   if (moveCamera) {
      off.z -= time*5.0;
      off.y += cos(time*0.5);
   }

   vec4 res = vec4(0.0);
   
#ifndef AA
   // position and ray direction
   vec2 p = gl_FragCoord.xy / resolution.xy;
   vec3 ro = cameraPos + off;
   vec3 rd = normalize(GetRay((cameraLookat-off)-cameraPos+off, p));
   
   // march ray
   float t = 0.0;
   float d = 0.0;
   for (int i=0; i<RAY_DEPTH; i++)
   {
      vec3 p = ro + rd * t;
      d = Dist(p);
      if (d < DISTANCE_MIN)
      {
         res = vec4(p, 1.0);  // HIT
         break;
      }
      // this is subtle - but makes a huge difference to the quality of the hit accuracy
      // ensure we walk a distance that is small enough to not miss the peaks as we get closer
      t += d * min( max(0.2, (p.y-d)), 0.8 );
      //t += d * 0.5;
      if (t > MAX_DEPTH) break;
   }
   
   if (res.a == 0.0)
   {
      res.rgb = Sky(ro, rd);
   }
   else
   {
      res.rgb = Shading(res.rgb, rd, CalcNormal(res.rgb, t), ro).rgb;
   }
#else
   vec2 p;
   float d_ang = 2.*PI / float(ANTIALIAS_SAMPLES);
   float ang = d_ang * 0.33333;
   float r = 0.3;
   for (int i = 0; i < ANTIALIAS_SAMPLES; i++)
   {
      p = vec2((gl_FragCoord.x + cos(ang)*r) / resolution.x, (gl_FragCoord.y + sin(ang)*r) / resolution.y);
      vec3 ro = cameraPos + off;
      vec3 rd = normalize(GetRay((cameraLookat-off)-cameraPos+off, p));
      vec4 _res = vec4(0.0);
      float t = 0.0;
      float d = 1.0;
      for (int i=0; i<RAY_DEPTH; i++)
      {
         vec3 p = ro + rd * t;
         d = Dist(p);
         if (d < DISTANCE_MIN)
         {
            _res = vec4(p, 1.0);  // HIT
            break;
         }
         t += d * 0.5;
         if (t > MAX_DEPTH) break;
      }
      
      if (_res.a == 0.0)
      {
         res.rgb += Sky(ro, rd);
      }
      else
      {
         res.rgb += Shading(_res.rgb, rd, CalcNormal(_res.rgb, t), ro).rgb;
      }
      ang += d_ang;
   }
   res.rgb /= float(ANTIALIAS_SAMPLES);
#endif
   
   if (postEffects) res.rgb = PostEffects(res.rgb, p);
   
   gl_FragColor = vec4(res.rgb, 1.0);
}
      </script>
      
      <script type="text/javascript">
var requestAnimationFrame = window.requestAnimationFrame || window.webkitRequestAnimationFrame ||
                            window.mozRequestAnimationFrame || window.msRequestAnimationFrame || 
                            function(c) {window.setTimeout(c, 15)};
var config = {
   camera: {
      x: 120.0, y: 5.0, z: 0.0
   },
   lookat: {
      x: 175.0, y: -35.0, z: -80.0
   },
   sunDir: {
      x: 5.2, y: 1.7, z: -3.0
   },
   sunColour: {
      r: 1.4, g: 1.0, b: 0.6
   },
   surface: {
      ambient: 0.2
   },
   global: {
      ao: false,
      shadows: true,
      postEffects: true,
      moveCamera: true,
      fog: true,
      fogB: 0.34,
      fogC: 0.21,
      peaks: 0.5,
      troughs: 0.75,
      offset: 1.35,
      lacunarity: 2.6,
      amplitude: 0.55,
      squeeze: 0.15,
      param: 0.15
   }
};

var pause = false;
function init()
{
   var img = new Image();
   img.onload = function() {
      loaded([img]);
   };
   img.src = "ggreynoise512.png";
}

var _images;
var aspect, gl;
function loaded(images)
{
   _images = images;
   document.addEventListener('keydown', function(e) {
      switch (e.keyCode)
      {
         case 32:
         case 27: // ESC
            pause = !pause;
            break;
      }
   }, false);

   var mobile = (navigator.userAgent.indexOf("Android") !== -1);
   
   // create webgl context on the canvas element
   var canvas = document.getElementById("canvas");
   aspect = canvas.width / canvas.height;
   try
   {
      gl = canvas.getContext("experimental-webgl");
   }
   catch (e)
   {
      document.write("Whoops! No useful WEB-GL impl available. Shame on you and your browser vendor.<br>" + e.message);
      return;
   }
   gl.viewport(0, 0, canvas.width, canvas.height);
   gl.clearColor(0, 0, 0, 1);
   gl.clear(gl.COLOR_BUFFER_BIT);
   
   // textures
   var noiseTexture = gl.createTexture();
   gl.bindTexture(gl.TEXTURE_2D, noiseTexture);
   gl.texImage2D(gl.TEXTURE_2D, 0, gl.RGBA, gl.RGBA, gl.UNSIGNED_BYTE, _images[0]);
   gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MAG_FILTER, gl.LINEAR);
   gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MIN_FILTER, gl.LINEAR_MIPMAP_NEAREST);
   gl.generateMipmap(gl.TEXTURE_2D);
   gl.bindTexture(gl.TEXTURE_2D, null);
   
   // get the vertex and fragment shader source
   var v = document.getElementById("vertex").firstChild.nodeValue;
   var f = document.getElementById("fragment").firstChild.nodeValue;
   
   // compile and link the shaders
   var vs = gl.createShader(gl.VERTEX_SHADER);
   gl.shaderSource(vs, v);
   gl.compileShader(vs);
   
   var fs = gl.createShader(gl.FRAGMENT_SHADER);
   gl.shaderSource(fs, f);
   gl.compileShader(fs);
   
   var program = gl.createProgram();
   gl.attachShader(program, vs);
   gl.attachShader(program, fs);
   gl.linkProgram(program);
   
   // debug shader compile status
   var error = false;
   if (!gl.getShaderParameter(vs, gl.COMPILE_STATUS))
   {
      error = true;
      console.log(gl.getShaderInfoLog(vs));
   }
   
   if (!gl.getShaderParameter(fs, gl.COMPILE_STATUS))
   {
      error = true;
      console.log(gl.getShaderInfoLog(fs));
   }
   
   if (!gl.getProgramParameter(program, gl.LINK_STATUS))
   {
      error = true;
      console.log(gl.getProgramInfoLog(program));
   }
   if (error) return;
   
   var firstTime = Date.now();
   (f = function() {
      if (!pause)
      {
         
         // create vertices to fill the canvas with a single quad 
         var vertices = new Float32Array(
            [
               -1, 1*aspect, 1, 1*aspect, 1, -1*aspect,
               -1, 1*aspect, 1, -1*aspect, -1, -1*aspect
            ]);
         
         var vbuffer = gl.createBuffer();
         gl.bindBuffer(gl.ARRAY_BUFFER, vbuffer);
         gl.bufferData(gl.ARRAY_BUFFER, vertices, gl.STATIC_DRAW);
         
         var triCount = 2,
             numItems = vertices.length / triCount;
         
         gl.useProgram(program);
         
         var time = (Date.now() - firstTime) / 1000.0;
         program.time = gl.getUniformLocation(program, "time");
         gl.uniform1f(program.time, time);
         
         program.resolution = gl.getUniformLocation(program, "resolution");
         gl.uniform2f(program.resolution, canvas.width, canvas.height);
         
         program.cameraPos = gl.getUniformLocation(program, "cameraPos");
         gl.uniform3f(program.cameraPos, config.camera.x, config.camera.y, config.camera.z);
         
         program.cameraLookat = gl.getUniformLocation(program, "cameraLookat");
         gl.uniform3f(program.cameraLookat, config.lookat.x, config.lookat.y, config.lookat.z);
         
         program.sunDir = gl.getUniformLocation(program, "sunDir");
         // pre normalise light dir
         var x = config.sunDir.x, y = config.sunDir.y, z = config.sunDir.z;
         var len = x*x + y*y + z*z;
         len = 1.0 / Math.sqrt(len);
         gl.uniform3f(program.sunDir, config.sunDir.x*len, config.sunDir.y*len, config.sunDir.z*len);
         
         program.sunColour = gl.getUniformLocation(program, "sunColour");
         gl.uniform3f(program.sunColour, config.sunColour.r, config.sunColour.g, config.sunColour.b);
         
         program.ambient = gl.getUniformLocation(program, "ambient");
         gl.uniform1f(program.ambient, config.surface.ambient);
         
         program.moveCamera = gl.getUniformLocation(program, "moveCamera");
         gl.uniform1f(program.moveCamera, config.global.moveCamera);
         
         program.postEffects = gl.getUniformLocation(program, "postEffects");
         gl.uniform1f(program.postEffects, config.global.postEffects);
         
         program.ao = gl.getUniformLocation(program, "ao");
         gl.uniform1f(program.ao, config.global.ao);
         
         program.shadows = gl.getUniformLocation(program, "shadows");
         gl.uniform1f(program.shadows, config.global.shadows);
         
         program.fog = gl.getUniformLocation(program, "fog");
         gl.uniform1f(program.fog, config.global.fog);
         
         program.fogB = gl.getUniformLocation(program, "fogB");
         gl.uniform1f(program.fogB, config.global.fogB);
         
         program.fogC = gl.getUniformLocation(program, "fogC");
         gl.uniform1f(program.fogC, config.global.fogC);
         
         program.snowLevel = gl.getUniformLocation(program, "snowLevel");
         gl.uniform1f(program.snowLevel, config.global.peaks);

         program.grassLevel = gl.getUniformLocation(program, "grassLevel");
         gl.uniform1f(program.grassLevel, config.global.troughs);

         program.offset = gl.getUniformLocation(program, "offset");
         gl.uniform1f(program.offset, config.global.offset);

         program.lacunarity = gl.getUniformLocation(program, "lacunarity");
         gl.uniform1f(program.lacunarity, config.global.lacunarity);

         program.amplitude = gl.getUniformLocation(program, "amplitude");
         gl.uniform1f(program.amplitude, config.global.amplitude);
         
         program.squeeze = gl.getUniformLocation(program, "squeeze");
         gl.uniform1f(program.squeeze, config.global.squeeze);
         
         program.param = gl.getUniformLocation(program, "param");
         gl.uniform1f(program.param, config.global.param);
         
         gl.bindTexture(gl.TEXTURE_2D, noiseTexture);
         gl.uniform1i(gl.getUniformLocation(program, "texture0"), 0);
         
         program.aVertexPosition = gl.getAttribLocation(program, "aVertexPosition");
         gl.enableVertexAttribArray(program.aVertexPosition);
         gl.vertexAttribPointer(program.aVertexPosition, triCount, gl.FLOAT, false, 0, 0);
         
         gl.drawArrays(gl.TRIANGLES, 0, numItems);
         
         //pause = true;
      }
      requestAnimationFrame(f);
   })();
}
      </script>
      <link rel="stylesheet" href="reset.css">
      <link rel="stylesheet" href="page.css">
   </head>
   <body onload="init()">
      <!--<canvas id="canvas" width="768" height="512"></canvas>-->
      <canvas id="canvas" width="1920" height="1080"></canvas>
      <!--<canvas id="canvas" width="3840" height="2160"></canvas>-->
   </body>

<!-- Mirrored from digitonaut.com/art/shader_lab_crater_terrain/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 10:33:01 GMT -->
</html>